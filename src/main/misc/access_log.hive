
ADD JAR /usr/lib/hive/lib/hive-contrib-0.7.1-cdh3u1.jar;

CREATE EXTERNAL TABLE access_log (
  host STRING,
  identity STRING,
  user STRING,
  time STRING,
  request STRING,
  status STRING,
  size STRING,
  referer STRING,
  agent STRING)
ROW FORMAT SERDE 'org.apache.hadoop.hive.contrib.serde2.RegexSerDe'
WITH SERDEPROPERTIES (
  "input.regex" = "([^ ]*) ([^ ]*) ([^ ]*) (-|\\[[^\\]]*\\]) ([^ \"]*|\"[^\"]*\") (-|[0-9]*) (-|[0-9]*)(?: ([^ \"]*|\"[^\"]*\") ([^ \"]*|\"[^\"]*\"))?",
  "output.format.string" = "%1$s %2$s %3$s %4$s %5$s %6$s %7$s %8$s %9$s"
)
STORED AS TEXTFILE LOCATION '/user/tchug/access_log/input';


SELECT * FROM access_log WHERE request LIKE '%=http%';


INSERT OVERWRITE DIRECTORY '/user/tchug/access_log/output-bad'
SELECT * FROM access_log WHERE request LIKE '%=http%';

INSERT OVERWRITE DIRECTORY 
'/user/tchug/access_log/output-day-result'
SELECT day, COUNT(1) AS cnt FROM 
(
  SELECT regexp_extract(time, '^\\[(\\d+/\\w+)/', 1) 
  AS day FROM access_log
) t1
GROUP BY day 
SORT BY cnt DESC 
LIMIT 10;


INSERT OVERWRITE DIRECTORY
'/user/tchug/access_log/output-session-result'
SELECT
  host, SUBSTRING(agent, 0, 5) AS agent,
  request, status, size
FROM
access_log
SORT BY host, agent;



